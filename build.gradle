buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.flywaydb:flyway-gradle-plugin:10.20.1"
        classpath "org.flywaydb:flyway-database-postgresql:10.20.1"
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.flywaydb.flyway' version '10.20.1'
}

group = 'com.rgs'
version = '0.0.2'

jar.enabled = false

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven { url = 'https://repo.spring.io/milestone' }
    maven { url = 'https://repo.spring.io/snapshot' }
}

dependencies {

    implementation 'org.mybatis:mybatis:3.5.15'
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.2'

    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'

    // postgres
    runtimeOnly 'org.postgresql:postgresql'
    testImplementation 'org.testcontainers:postgresql'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // Spring Security
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // OAuth2 Client
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    // Flyway
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-database-postgresql'

    // QueryDSL
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    implementation 'com.querydsl:querydsl-sql:5.0.0'
    implementation "com.querydsl:querydsl-core:5.0.0"
    implementation "com.querydsl:querydsl-collections:"
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

}


// Flyway 설정 - 환경별 데이터베이스 마이그레이션 관리
flyway {
    // 데이터베이스 연결 설정 (기본값: local 환경)
    url = project.findProperty("flywayUrl") ?: "jdbc:postgresql://localhost:5432/localdb"  // local 환경 Docker DB
    user = project.findProperty("flywayUser") ?: "localuser"  // local 환경 기본 사용자
    password = project.findProperty("flywayPassword") ?: "localpass"  // local 환경 기본 비밀번호
    
    // 스키마 및 마이그레이션 파일 위치 설정
    schemas = (project.findProperty("flywaySchemas") ?: "public").split(",")
    locations = ['classpath:db/migration']  // 마이그레이션 SQL 파일 위치
    encoding = 'UTF-8'
    // Clean 명령 비활성화 설정 (환경별 제어)
    // local: false (개발시 DB 초기화 허용)
    // dev/prod: true (운영 DB 보호)
    cleanDisabled = (project.findProperty("flywayCleanDisabled") ?: "false").toBoolean()

    // 환경별 설정 가이드:
    // LOCAL: ./gradlew flywayMigrate (Docker DB, 초기화 허용)
    // DEV: ./gradlew flywayMigrate -PflywayUrl=DEV_DB_URL -PflywayUser=DEV_USER -PflywayPassword=DEV_PASS -PflywayCleanDisabled=true
}

tasks.named('test') {
    useJUnitPlatform()
}


def querydslDir = layout.buildDirectory.dir('generated/querydsl').get().asFile

tasks.withType(JavaCompile) {
    options.generatedSourceOutputDirectory = file(querydslDir)
}

sourceSets {
    main {
        java {
            srcDirs += [querydslDir]
        }
    }
}

tasks.named('clean') {
    doLast {
        querydslDir.deleteDir()
    }
}
